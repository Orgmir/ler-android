// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.buildConfig = [
        'compileSdk': 30,
        'minSdk'    : 23,
        'targetSdk' : 30,

        'version'   : [
            'major': 1,
            'minor': 0,
            'patch': 2,
            'build': 0,
        ],
    ]

    ext.buildConfig.version['build'] = project.findProperty("buildNumber")?.toInteger() ?: 0
    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext {
        kotlin_version = '1.4.10'
        sqldelight_version = '1.4.3'
        coroutines_version = '1.3.9'
    }

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0-alpha13'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "com.squareup.sqldelight:gradle-plugin:${sqldelight_version}"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}